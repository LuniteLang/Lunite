import "io.lun" as io
import "lexer.lun" as lex
import "parser.lun" as parser
import "semantic.lun" as sem
import "codegen_c.lun" as codegen
import "string.lun" as str
import "sys.lun" as sys
import "list.lun" as list

fn main() {
    let argc = sys.get_argc();
    if argc < 2 {
        print_usage();
    }
    
    if argc >= 2 {
        dispatch_command();
    }
}

fn print_usage() {
    str.println("Lunite Self-Hosted Compiler");
    str.println("Usage: lnc [command] [file]");
    str.println("Commands:");
    str.println("  check [file]  - Check syntax and types");
    str.println("  build [file]  - Generate C code");
}

fn dispatch_command() {
    let command = sys.get_arg(1);
    let first_char = str.char_at(command, 0);
    let argc = sys.get_argc();
    
    if first_char == 99 {
        do_check(argc);
    }
    
    if first_char == 98 {
        do_build(argc);
    }
}

fn do_check(argc: int) {
    if argc < 3 {
        str.println("Error: Missing filename");
    }
    
    if argc >= 3 {
        run_check(sys.get_arg(2));
    }
}

fn do_build(argc: int) {
    if argc < 3 {
        str.println("Error: Missing filename");
    }
    
    if argc >= 3 {
        run_build(sys.get_arg(2));
    }
}

fn run_check(filename: string) {
    str.print("Checking: ");
    str.println(filename);

    let source = io.read_file_text(filename);
    str.println("--- Lexing ---");
    let l = lex.Lexer::new(source);
    
    str.println("--- Parsing ---");
    let p = parser.Parser::new(l);
    let program = p.parse_program();
    
    str.println("--- Semantic Analysis ---");
    let analyzer = sem.SemanticAnalyzer::new();
    let typed_items = analyzer.analyze_program(program);
    
    str.print("Analysis complete. Items found: ");
    str.println(str.int_to_string(typed_items.len()));
    
    str.println("Check passed!");
}

fn run_build(filename: string) {
    str.print("Building: ");
    str.println(filename);

    let source = io.read_file_text(filename);
    str.println("--- Lexing ---");
    let l = lex.Lexer::new(source);
    
    str.println("--- Parsing ---");
    let p = parser.Parser::new(l);
    let program = p.parse_program();
    
    str.println("--- Semantic Analysis ---");
    let analyzer = sem.SemanticAnalyzer::new();
    let typed_items = analyzer.analyze_program(program);
    
    str.println("--- Code Generation ---");
    let gen = codegen.CodegenC::new();
    let c_code = gen.generate(program);
    
    let base_len = str.length(filename) - 4;
    let out_name = str.concat(str.sub(filename, 0, base_len), ".c");
    io.write_file_text(out_name, c_code);
    
    str.print("Generated: ");
    str.println(out_name);
    
    str.println("Build complete!");
}
